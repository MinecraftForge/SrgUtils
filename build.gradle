import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
    alias libs.plugins.versions
}

final projectDisplayName = 'SRG Utils'
final projectArtifactId = base.archivesName = 'srgutils'
final projectVendor = 'Forge Development LLC'
description = 'Library for managing java obfuscation mappings in various formats.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)

    withSourcesJar()
}

dependencies {
    implementation libs.fastcsv

    //region Static Analysis
    compileOnly libs.nulls
    compileOnly libs.jsr305
    //endregion

    //region Testing
    testImplementation libs.bundles.junit
    testImplementation libs.powermock
    //endregion
}

test {
    useJUnitPlatform()
}

changelog {
    from '0.4'
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Automatic-Module-Name': 'net.minecraftforge.srgutils'
        ])
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/srgutils')
    }

    archiveClassifier = 'slim'
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = ''
    enableRelocation = true
    relocationPrefix = 'net.minecraftforge.srgutils.shadow'
}

artifacts {
    archives shadowJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = projectArtifactId

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

// TODO [SrgUtils] What is this?
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

idea.module { downloadSources = downloadJavadoc = true }
