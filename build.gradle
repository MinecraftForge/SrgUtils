buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

group 'net.minecraftforge'
version = gradleutils.getTagOffsetVersion()
println('Version: ' + version)

repositories {
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net/' }
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.2')
    testImplementation('org.powermock:powermock-core:2.0.9')
    compileOnly('com.google.code.findbugs:jsr305:3.0.2')
}

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

changelog {
    fromTag '0.4'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'SRG Utils'
                description = 'Library for managing java obfuscation mappings in various formats.'
                url = 'https://github.com/MinecraftForge/SrgUtils'
                scm {
                    url = 'https://github.com/MinecraftForge/SrgUtils'
                    connection = 'scm:git:git://github.com/MinecraftForge/SrgUtils.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/SrgUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/SrgUtils/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

